import dependencies.Dep
import dependencies.Modules

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'com.google.gms.google-services'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    namespace 'com.mashup.ssamd'
    compileSdk 33

    defaultConfig {
        applicationId "com.mashup.ssamd"
        minSdk 24
        targetSdk 33
        versionCode 2
        versionName "1.1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        lintConfig = file("$rootDir/lint.xml")
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            manifestPlaceholders = ["appName": "@string/app_name_debug"]
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
            signingConfig signingConfigs.debug
            buildConfigField "boolean", "IS_DEBUG", "true"
        }

        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            manifestPlaceholders = ["appName": "@string/app_name"]
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField "boolean", "IS_DEBUG", "false"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        dataBinding true
    }
}

dependencies {
    implementation(project(Modules.data))
    implementation(project(Modules.domain))
    implementation(project(Modules.presentation))

    implementation Dep.AndroidX.Core.core
    implementation Dep.AndroidX.appCompat
    implementation Dep.Google.material
    implementation Dep.AndroidX.constraintLayout

    testImplementation Dep.UnitTest.junit
    androidTestImplementation Dep.AndroidTest.junit
    androidTestImplementation Dep.AndroidTest.espresso

    // hilt
    implementation Dep.Dagger.Hilt.hilt
    kapt Dep.Dagger.Hilt.compiler

    // timber - log
    implementation Dep.Logger.timber

    // kakao login
    implementation Dep.Kakao.sdk

    // firebase
    implementation platform(Dep.Firebase.firebaseBom)
    implementation Dep.Firebase.cloudMessaging
}
